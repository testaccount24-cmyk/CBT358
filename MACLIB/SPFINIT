         MACRO
&LABEL   SPFINIT  &USER=N,&TIME=N,&TERM=N,&TABLES=N
.*---------------------------------------------------------------------
.*       THIS MACRO PERFORMS VARIOUS INITIALIZATION FUNCTIONS FOR AN
.*       SPF DIALOG MANAGER.
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
         LCLA  &N,&D,&B,&X
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.*---------------------------------------------------------------------
&LABEL   CSECT
         B     32(R15)             BRANCH AROUND
         DC    CL8'&LABEL'         CSECT NAME
         DC    C'-'
         DC    CL8'&SYSDATE'       COMPILE DATE
         DC    C'-'
         DC    CL8'&SYSTIME'       COMPILE TIME
         CNOP  0,4                 ALIGNMENT
.*---------------------------------------------------------------------
         STM   R14,R12,12(R13)     SAVE REGISTERS
&N       SETA  N'&SYSLIST
&D       SETA  1
&B       SETA  0
&X       SETA  &SYSNDX
         AIF   (&N NE 0).AAA
         MNOTE 8,'BASE REGISTER MISSING'
         MEXIT
.AAA     ANOP
         USING &LABEL,&SYSLIST(&D)
         LR    &SYSLIST(&D),R15    FIRST BASE REGISTER
.BBB     ANOP
&B       SETA  &B+4096
&D       SETA  &D+1
         AIF   (&D EQ &N+1).CCC
         USING &LABEL+&B,&SYSLIST(&D)
         LA    &SYSLIST(&D),2048(&SYSLIST(&D-1))
         LA    &SYSLIST(&D),2048(&SYSLIST(&D))
         AGO   .BBB
.CCC     CNOP  0,4
         B     *+76
SAVE&X   DC    18F'0'
         ST    R13,SAVE&X+4        SAVE BACK CHAIN
         MVC   8(4,R13),=A(SAVE&X) SET FORWARD CHAIN
         LA    R13,SAVE&X          THIS SAVE AREA
.*---------------------------------------------------------------------
.*       STANDARD VARIABLES
.*---------------------------------------------------------------------
         SPFVDEF 24,(SMSG)
         SPFVDEF 72,(LMSG)
         SPFVDEF 60,(ZCMD)
         SPFVGET (ZCMD),SHARED
         AIF   ('&USER' EQ 'N').NOUSER
.*---------------------------------------------------------------------
.*       USER RELATED VARIABLES   CODE:  USER=Y
.*---------------------------------------------------------------------
         SPFVDEF 44,(ZTEMPF)
         SPFVDEF 8,(ZPREFIX,ZUSER,ZLOGON)
         SPFVGET (ZTEMPF,ZPREFIX,ZUSER,ZLOGON),SHARED
.NOUSER  ANOP
         AIF   ('&TIME' EQ 'N').NOTIME
.*---------------------------------------------------------------------
.*       DATE/TIME VARIABLES      CODE:  TIME=Y
.*---------------------------------------------------------------------
         SPFVDEF 8,(ZDATE)
         SPFVDEF 6,(ZJDATE)
         SPFVDEF 5,(ZTIME)
         SPFVDEF 2,(ZMONTH,ZDAY,ZYEAR)
         SPFVGET (ZDATE,ZJDATE,ZTIME,ZMONTH,ZDAY,ZYEAR),SHARED
.NOTIME  ANOP
         AIF   ('&TERM' EQ 'N').NOTERM
.*---------------------------------------------------------------------
.*       TERMINAL VARIABLES       CODE:  TERM=Y
.*---------------------------------------------------------------------
         SPFVDEF 4,(ZTERM)
         SPFVDEF 2,(ZKEYS)
         SPFVGET (ZTERM,ZKEYS),SHARED
         SPFVDEF 48,(ZPF13,ZPF14,ZPF15,ZPF16,ZPF17,ZPF18,ZPF19,ZPF20,  +
               ZPF21,ZPF22,ZPF23,ZPF24)
         SPFVGET (ZPF13,ZPF14,ZPF15,ZPF16,ZPF17,ZPF18,ZPF19,ZPF20,     +
               ZPF21,ZPF22,ZPF23,ZPF24),SHARED
*        CLC   ZKEYS,=F'12'
*        BE    MAAA&X
         SPFVDEF 48,(ZPF01,ZPF02,ZPF03,ZPF04,ZPF05,ZPF06,ZPF07,ZPF08,  +
               ZPF09,ZPF10,ZPF11,ZPF12)
         SPFVGET (ZPF01,ZPF02,ZPF03,ZPF04,ZPF05,ZPF06,ZPF07,ZPF08,     +
               ZPF09,ZPF10,ZPF11,ZPF12),SHARED
.NOTERM  ANOP
         AIF   ('&TABLES' EQ 'N').NOTBL
.*---------------------------------------------------------------------
.*       TABLE VARIABLES          CODE:  TABLES=Y
.*---------------------------------------------------------------------
         SPFVDEF 4,(ZTDTOP),TYPE=FIXED
.*---------------------------------------------------------------------
.NOTBL   ANOP
MAAA&X   DS    0H
         L     R1,SAVE&X+4         PICK UP BACK CHAIN
         LM    R0,R1,20(R1)        RESTORE PARAMETER REGISTERS
         MEND
