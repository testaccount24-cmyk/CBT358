         MACRO
&NAME    ALCLRECL &LRECL=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'LRECL' KEYWORD
.*
.*       LRECL='NNNNN'          NUMERIC VALUE - MAX 32767
.*       LRECL=NAME             NAME  DC  H'NNNNN'  HALFWORD
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
         LCLA  &X
         LCLC  &LIT
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        LRECL
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALLRECL        KEY FOR LRECL
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'2'     MOVE 2 TO TXT UNIT LNGTH FLD
         AIF   ('&LRECL'(1,1) EQ '''').AAA
         CLC   &LRECL,=H'0'       ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         MVC   S99TUPAR(2),&LRECL LRECL
         AGO   .BBB
.AAA     ANOP
&LIT     SETC  '&LRECL'(2,K'&LRECL-2)
         MVC   S99TUPAR(2),=AL2(&LIT)   LRECL
.BBB     ANOP
         LA    R14,8(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
         MEND
