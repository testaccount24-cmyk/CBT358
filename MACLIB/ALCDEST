         MACRO
&NAME    ALCDEST &DEST=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'DEST' KEYWORD
.*
.*       DEST='R20'             DESTINATION ID, R0 - R255
.*       DEST=NAME              NAME  DC  CL4'R255'
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
         LCLA  &X
         LCLC  &LIT,&LL
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        REMOTE DEST ID
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALSUSER        KEY FOR DESTINATION
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 0 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'8'     MOVE 8 TO TXT UNIT LNGTH FLD
         AIF   ('&DEST'(1,1) EQ '''').AAA
         CLI   &DEST,C' '         ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         MVC   S99TUPAR(8),=CL8' '
&LL      SETC  'L''&DEST'
         MVC   S99TUPAR(&LL),&DEST
         AGO   .BBB
.AAA     ANOP
&LIT     SETC  '&DEST'(2,K'&DEST-2)
         MVC   S99TUPAR(8),=CL8'&LIT'  DESTNAME
.BBB     ANOP
         LA    R14,14(R14)        BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
         MEND
