         MACRO
&NAME    SPFSUB  &DSN,&MBR
.*---------------------------------------------------------------------
.*       THIS MACRO WILL INVOKE THE TSO SUBMIT COMMAND FOR A
.*       SEQUENTIAL DATA SET OR A PDS AND MEMBER.
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLA  &X
&X       SETA  &SYSNDX
*----------------------------------------------------------------------
*        SUBMIT THE JOB
*----------------------------------------------------------------------
         MVC   SPFDYNAM(256),BLANKS   SET TO BLANKS
         LA    R14,SPFDYNAM
         MVC   0(11,R14),=C'CMD(SUBMIT '
         LA    R14,11(R14)            BUMP
         MVI   0(R14),X'7D'           ADD A QUOTE
         LA    R14,1(R14)             BUMP
         MVC   0(44,R14),&DSN         MOVE THE DSN
         AIF   (T'&MBR EQ 'O').AAA
         XC    SPFTRT,SPFTRT          CLEAR
         MVI   SPFTRT+C' ',X'FF'      SET FOR A BLANK
         TRT   0(45,R14),SPFTRT       FIND END OF DSN
         LR    R14,R1                 COPY ADDR
         MVI   0(R14),C'('            ADD THE PAREN
         MVC   1(8,R14),&MBR          MOVE THE MEMBER NAME
         TRT   0(10,R14),SPFTRT       FIND END
         MVI   0(R1),C')'             ADD A PAREN
         MVI   1(R1),X'7D'            ADD A QUOTE
         AGO   .BBB
.AAA     ANOP
         XC    SPFTRT,SPFTRT          CLEAR
         MVI   SPFTRT+C' ',X'FF'      SET FOR A BLANK
         TRT   0(45,R14),SPFTRT       FIND END OF DSN
         MVC   0(2,R1),=X'7D5D'       ADD A QUOTE PAREN
.BBB     ANOP
         MVC   SPFDYNIN,=F'100'
         CALL  ISPLINK,(SELECT,SPFDYNIN,SPFDYNAM),VL
         MEND
