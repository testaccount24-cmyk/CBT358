         MACRO
&NAME    SPFTAILR &PROC,&SUBMIT,&JOBCARD=
.*---------------------------------------------------------------------
.*       THIS MACRO WILL INVOKE SPF FILE TAILORING SERVICES AND
.*       OPTIONALLY SUBMIT THE JOB. IT WILL ALSO UPDATE THE JOBNAME
.*       SUFFIX IF REQUESTED.
.*
.*       MARCH, 1984 - MOVED THIS VERSION IN UNTIL I GET AN EXPLANATION
.*       ON THE NEW VERSION. R. WILLMS
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLA  &X
         LCLC  &LIT
&X       SETA  &SYSNDX
         AIF   (T'&PROC NE 'O').AAA
         MNOTE 8,'SPF SKELETON NOT SPECIFIED'
         MEXIT
.AAA     ANOP
         AIF   ('&PROC'(1,1) EQ '''').BBB
         MVC   PROC,&PROC           MOVE SKELETON NAME
         AGO   .CCC
.BBB     ANOP
&LIT     SETC  '&PROC'(2,K'&PROC-2)
         MVC   PROC,=CL8'&LIT'
.CCC     ANOP
         CALL  ISPLINK,(FTOPEN,TEMP),VL
         CALL  ISPLINK,(FTINCL,PROC),VL
         CALL  ISPLINK,(FTCLOSE),VL
         AIF   (T'&SUBMIT EQ 'O').DONE
         SPFSUB ZTEMPF,,CMNB@JNU
         AIF   (T'&JOBCARD EQ 'O').DONE
         SPFJOBNM &JOBCARD
.DONE    ANOP
         MEND
