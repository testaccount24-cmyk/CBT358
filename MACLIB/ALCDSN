         MACRO
&NAME    ALCDSN  &DSN=
         LCLA  &X
         LCLC  &LIT,&LA
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        DSNAME
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALDSNAM        KEY FOR DSNAME
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'44'    MOVE 44 TO TXT UNIT LNGTH FLD
         AIF   ('&DSN'(1,1) EQ '''').AAA
         AIF   ('&DSN'(1,1) EQ '(').BBB
         CLI   &DSN,C' '          ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         MVC   S99TUPAR(44),=CL44' '
&LA      SETC  'L''&DSN'
         MVC   S99TUPAR(&LA),&DSN  DSNAME
         AGO   .CCC
.AAA     ANOP
&LIT     SETC  '&DSN'(2,K'&DSN-2)
         MVC   S99TUPAR(44),=CL44'&LIT'
         AGO   .CCC
.BBB     ANOP
         L     R1,SPFREGS+16       PICK UP ADDR OF DSN FIELD
         MVC   S99TUPAR(44),0(R1)
.CCC     ANOP
         LA    R14,50(R14)        BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
         MEND
