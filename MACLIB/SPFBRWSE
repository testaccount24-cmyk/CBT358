         MACRO
&NAME    SPFBRWSE &IDS,&MBR,&VOL
.*---------------------------------------------------------------------
.*       THIS MACRO INVOKES SPF BROWSE FOR A DATA SET OR DATA SET AND
.*       MEMBER.
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLC  &LIT,&REG,&LA
         LCLA  &X
&X       SETA  &SYSNDX
         MVC   SPFDYNAM,BLANKS         TO BLANKS
         MVI   SPFDYNAM,X'7D'          ADD A QUOTE
         AIF   ('&IDS'(1,1) EQ '''').AAA
         AIF   ('&IDS'(1,1) EQ '(').REG1
&LA      SETC  'L''&IDS'
         MVC   SPFDYNAM+1(&LA),&IDS    MOVE DSN
         AGO   .BBB
.AAA     ANOP
&LIT     SETC  '&IDS'(2,K'&IDS-2)
         MVC   SPFDYNAM+1(54),=CL54'&LIT' MOVE DSN
         AGO   .BBB
.REG1    ANOP
&REG     SETC  '&IDS'(2,K'&IDS-2)
         MVC   SPFDYNAM+1(54),0(&REG)  MOVE DSN
.BBB     ANOP
         LA    R14,SPFDYNAM            START OF FIELD
         XC    SPFTRT,SPFTRT           CLEAR TRT TABLE
         MVI   SPFTRT+C' ',X'FF'       SET FOR A BLANK
         TRT   0(56,R14),SPFTRT        FIND END
         BNZ   MAAA&X                  FOUND IT
         LA    R1,55(R14)              SET DEFAULT
MAAA&X   EQU   *
.*-------------------------------------------------------------------
         AIF   (T'&MBR EQ 'O').EEE
         MVI   0(R1),C'('              ADD A PAREN
         LA    R14,1(R1)               BUMP
         AIF   ('&MBR'(1,1) EQ '''').CCC
         AIF   ('&MBR'(1,1) EQ '(').REG2
         CLI   &MBR,C' '               ANY SPECIFIED ?
         BNH   MCCC&X                  NO - SKIP IT
&LA      SETC  'L''&MBR'
         MVC   0(&LA,R14),&MBR         MOVE MBR NAME
         AGO   .DDD
.CCC     ANOP
&LIT     SETC  '&MBR'(2,K'&MBR-2)
         MVC   0(8,R14),=CL8'&LIT'     MOVE MBR NAME
         AGO   .DDD
.REG2    ANOP
&REG     SETC  '&MBR'(2,K'&MBR-2)
         MVC   0(8,R14),0(&REG)        MOVE MBR NAME
.DDD     ANOP
         TRT   0(8,R14),SPFTRT         FIND END
         BNZ   MBBB&X                  FOUND IT
         LA    R1,8(R14)               SET DEFAULT
MBBB&X   EQU   *
         MVI   0(R1),C')'              ADD A PAREN
         LA    R1,1(R1)                BUMP
MCCC&X   EQU   *
.EEE     ANOP
         MVI   0(R1),X'7D'             ADD ENDING QUOTE
.*-------------------------------------------------------------------
         MVC   SPFVOL,BLANKS
         AIF   (T'&VOL EQ 'O').FFF
         MVC   SPFVOL,&VOL
.FFF     ANOP
         CALL  ISPLINK,(BROWSE,SPFDYNAM,SPFVOL),VL
         B     MZZZ&X                  GO AROUND CONSTANT
MZZZ&X   DS    0H
         MEND
