         MACRO
&NAME    ALCBLKSZ &BLKSIZE=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'BLKSIZE' KEYWORD
.*
.*       BLKSIZE='NNNNN'        NUMERIC VALUE - MAX 32767
.*       BLKSIZE=NAME           NAME  DC  H'NNNNN'  (HALFWORD)
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
         LCLA  &X
         LCLC  &LIT
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        BLOCK SIZE
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALBLKSZ        KEY FOR BLKSIZE
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'2'     MOVE 2 TO TXT UNIT LNGTH FLD
         AIF   ('&BLKSIZE'(1,1) EQ '''').AAA
         CLC   &BLKSIZE,=H'0'     ZERO ?
         BE    MZZZ&X             YES - SKIP IT
         MVC   S99TUPAR(2),&BLKSIZE BLKSIZE
         AGO   .BBB
.AAA     ANOP
&LIT     SETC  '&BLKSIZE'(2,K'&BLKSIZE-2)
         MVC   S99TUPAR(2),=AL2(&LIT)   BLKSIZE
.BBB     ANOP
         LA    R14,8(R14)         BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
         MEND
