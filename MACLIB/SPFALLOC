         MACRO
&NAME    SPFALLOC &FUNC,&DD=,&DSN=,&MBR=,&DISP=,&SPACE=,&VOL=,&UNIT=,  +
               &RECFM=,&BLKSIZE=,&LRECL=,&SYSOUT=,&COPIES=,&FCB=,      +
               &DEST=,&FREE=,&TERM=,&DUMMY=,&RETDD=,&MODEL=,           +
               &CCDD=,&RLSE=,&HOLD=
         GBLA  &SPF1
         GBLC  &XXXFUNC                                     RJW
         LCLA  &X
         LCLC  &REGD
&XXXFUNC SETC  '&FUNC'                                      RJW
&X       SETA  &SYSNDX
&SPF1    SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*   R2 - TRT
*   R3 - FIELD POINTER
*   R4 - DYNAM REQUEST BLOCK
*   R14 - DYNAMIC TEXT UNITS
*   R15 - DYNAMIC TEXT POINTERS
*        INITIALIZATION
*----------------------------------------------------------------------
         STM   R2,R5,SPFREGS      SAVE REGISTERS
         AIF   (T'&DSN EQ 'O').REG1
         AIF   ('&DSN'(1,1) NE '(').REG1
&REGD    SETC  '&DSN'(2,K'&DSN-2)
         ST    &REGD,SPFREGS+16
.REG1    ANOP
         AIF   (T'&MBR EQ 'O').REG2
         AIF   ('&MBR'(1,1) NE '(').REG2
&REGM    SETC  '&MBR'(2,K'&MBR-2)
         ST    &REGM,SPFREGS+20
.REG2    ANOP
         LA    R0,SPFDYNAM        ADDR OF WORKAREA
         LA    R1,SPFDYNLL        LENGTH OF WORKAREA
         LA    R14,*              DUMMY ADDR
         LA    R15,0              LENGTH AND PAD CHARACTER
         MVCL  R0,R14             CLEAR WORKAREA
         LA    R15,SPFDYNAM       ADDR OF DYN WORKAREA
         USING S99RBP,R15         REQ BLK POINTER DSECT
         LA    R4,S99RBP+4        ADDRESSABILITY OF RB DSECT
         USING S99RB,R4           RB DSECT
         ST    R4,S99RBPTR        MAKE RBPTR POINT TO RB
         OI    S99RBPTR,S99RBPND  TURN ON HOB IN RBPTR
         MVI   S99RBLN,RBLEN      PUT LEN IN ITS LENGTH FIELD
         LA    R15,S99RB+RBLEN    POINT BEYOND RB (1ST TEXT PTR)
         USING S99TUPL,R15        ADDRESSABILITY OF TEXT UNIT POINTERS
         ST    R15,S99TXTPP       INIT TEXT PTR IN RB
         LA    R14,S99TUPL+128    POINT BEYOND LAST TXT PTR (1ST TXT
         USING S99TUNIT,R14       ADDRESSABILITY OF TEXT UNITS
*----------------------------------------------------------------------
*        SET FUNCTION
*----------------------------------------------------------------------
         AIF   ('&FUNC' EQ 'A').AAA
         AIF   ('&FUNC' EQ 'D').BBB
         AIF   ('&FUNC' EQ 'C').CCC
         MNOTE 8,'INVALID FUNCTION CODE'
         MEXIT
.AAA     ANOP
         MVI   S99VERB,S99VRBAL   SET VERB CODE TO ALLOCATE
         AGO   .ALLOC
.BBB     ANOP
         MVI   S99VERB,S99VRBUN   SET VERB CODE TO DE-ALLOCATE
         ALCUNAL
         AGO   .ALLOC
.CCC     ANOP
         MVI   S99VERB,S99VRBCC   SET VERB CODE TO CONCATENATE
         AGO   .CONCAT
.ALLOC   ANOP
         AIF   (T'&DD EQ 'O').NODD
         ALCDD DD=&DD
.NODD    ANOP
         AIF   (T'&DSN EQ 'O').NODSN
         ALCDSN DSN=&DSN
.NODSN   ANOP
         AIF   (T'&MBR EQ 'O').NOMBR
         ALCMBR MBR=&MBR
.NOMBR   ANOP
         AIF   (T'&DISP EQ 'O').NODISP
         ALCDISP DISP=&DISP
.NODISP  ANOP
         AIF   (T'&SPACE EQ 'O').NOSPACE
         ALCSPACE SPACE=&SPACE
.NOSPACE ANOP
         AIF   (T'&VOL EQ 'O').NOVOL
         ALCVOL VOL=&VOL
.NOVOL   ANOP
         AIF   (T'&UNIT EQ 'O').NOUNIT
         ALCUNIT UNIT=&UNIT
.NOUNIT  ANOP
         AIF   (T'&BLKSIZE EQ 'O').NOBLKSZ
         ALCBLKSZ BLKSIZE=&BLKSIZE
.NOBLKSZ ANOP
         AIF   (T'&LRECL EQ 'O').NOLRECL
         ALCLRECL LRECL=&LRECL
.NOLRECL ANOP
         AIF   (T'&RECFM EQ 'O').NORECFM
         ALCRECFM RECFM=&RECFM
.NORECFM ANOP
         AIF   (T'&SYSOUT EQ 'O').NOSOUT
         ALCSSOUT SYSOUT=&SYSOUT
.NOSOUT  ANOP
         AIF   (T'&DEST EQ 'O').NODEST
         ALCDEST DEST=&DEST
.NODEST  ANOP
         AIF   (T'&COPIES EQ 'O').NOCOPY
         ALCCOPYS COPIES=&COPIES
.NOCOPY  ANOP
         AIF   (T'&FCB EQ 'O').NOFCB
         ALCFCB FCB=&FCB
.NOFCB   ANOP
         AIF   (T'&FREE EQ 'O').NOFREE
         ALCFREE FREE=&FREE
.NOFREE  ANOP
         AIF   (T'&TERM EQ 'O').NOTERM
         ALCTERM TERM=&TERM
.NOTERM  ANOP
         AIF   (T'&DUMMY EQ 'O').NODUMMY
         ALCDUMMY DUMMY=&DUMMY
.NODUMMY ANOP
         AIF   (T'&RETDD EQ 'O').NORETDD
         ALCRETDD RETDD=&RETDD
.NORETDD ANOP
         AIF   (T'&MODEL EQ 'O').NOMODEL
         ALCMODEL MODEL=&MODEL
.NOMODEL ANOP
         AIF   (T'&RLSE EQ 'O').NORLSE
         ALCRLSE RLSE=&RLSE
.NORLSE  ANOP
         AIF   (T'&HOLD EQ 'O').NOHOLD
         ALCHOLD HOLD=&HOLD
.NOHOLD  ANOP
         AGO   .DDD
.CONCAT  ANOP
         AIF   (T'&CCDD EQ 'O').NOCCDD
         ALCCC CCDD=&CCDD
         AGO   .DDD
.NOCCDD  ANOP
         AIF   (T'&CCDSN EQ 'O').DDD
         ALCCC CCDSN=&CCDSN
         AGO   .DDD
.DDD     ANOP
         S     R15,=F'4'
         OI    S99TUPTR,X'80'     END OF LIST
         LA    R1,SPFDYNAM
         MVC   SPFDYN15,=4C'0'    INITIALIZE
         MVC   SPFDYNER,=4C'0'    INITIALIZE
         MVC   SPFDYNIN,=4C'0'    INITIALIZE
         SVC   99
         ALCERR &FUNC,DD=&DD,DSN=&DSN
.EEE     ANOP
         AIF   (T'&RETDD EQ 'O').ZZZ
         AIF   ('&RETDD'(1,1) EQ '(').FFF
         MVC   &RETDD.(8),0(R5)   MOVE RETURNED DDNAME
         AGO   .ZZZ
.FFF     ANOP
&REGD    SETC  '&RETDD'(2,K'&RETDD-2)
         MVC   0(8,&REGD),0(R5)
.ZZZ     ANOP
MZZZ&SPF1 EQU   *
         LM    R2,R5,SPFREGS
         MEND
