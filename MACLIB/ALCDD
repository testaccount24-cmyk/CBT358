         MACRO
&NAME    ALCDD &DD=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       'DD' KEYWORD
.*
.*       DD='DDNAME'            CHARACTER - 1 - 8 BYTES
.*       DD=NAME                NAME  DC  CL5'SYSUT'   1 - 8 BYTES
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
         LCLA  &X
         LCLC  &LIT,&REG,&LA
&X       SETA  &SYSNDX
&NAME    DS    0H
*----------------------------------------------------------------------
*        DDNAME
*----------------------------------------------------------------------
         STCM  R14,15,S99TUPTR    TEXT UNIT POINTER
         LA    R1,DALDDNAM        KEY FOR DDNAME
         STCM  R1,3,S99TUKEY      PUT IN TEXT UNIT KEY FIELD
         MVC   S99TUNUM,=H'1'     MOVE 1 TO TXT UNIT NUMBR FLD
         MVC   S99TULNG,=H'8'     MOVE 8 TO TXT UNIT LNGTH FLD
         AIF   ('&DD'(1,1) EQ '''').AAA
         AIF   ('&DD'(1,1) EQ '(').BBB
         CLI   &DD,C' '           ANY SPECIFIED ?
         BNH   MZZZ&X             NO - SKIP IT
         MVC   S99TUPAR(8),=CL8' '
&LA      SETC  'L''&DD'
         MVC   S99TUPAR(&LA),&DD DDNAME
         AGO   .CCC
.AAA     ANOP
&LIT     SETC  '&DD'(2,K'&DD-2)
         MVC   S99TUPAR(8),=CL8'&LIT'
         AGO   .CCC
.BBB     ANOP
&REG     SETC  '&DD'(2,K'&DD-2)
         MVC   S99TUPAR(8),0(&REG)
.CCC     ANOP
         LA    R14,14(R14)        BUMP TO NEXT TXT UNIT
         LA    R15,4(R15)         BUMP TO NEXT TXT UNIT PTR
MZZZ&X   EQU   *
         MEND
