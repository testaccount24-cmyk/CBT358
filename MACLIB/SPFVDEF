         MACRO
&NAME    SPFVDEF &LEN,&FLD,&TYPE=CHAR,&DC=YES
.*---------------------------------------------------------------------
.*       THIS MACRO INVOKES THE SPF VDEFINE SERVICE.
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
&NAME    DS    0H
         GBLC  &VDEF1
         LCLA  &X,&B,&N,&LL
&X       SETA  &SYSNDX
         AIF   ('&VDEF1' EQ 'NO').AAA
&VDEF1   SETC  'NO'
         CNOP  0,4
         B     *+8
ISPLEN   DC    A(0)       LENGTH FIELD
.AAA     ANOP
&N       SETA  N'&FLD
         MVC   ISPLEN,=A(&LEN)
         CALL  ISPLINK,(VDEFINE,FLD&X,&FLD(1),&TYPE,ISPLEN),VL
         B     MAAA&X
&B       SETA  1
         AIF   (&N GT 1).BBB
&LL      SETA  K'&FLD(1)+2
FLD&X    DC    CL&LL'(&FLD(1))'
         AGO   .EEE
.*--------------------------------------------------------------------
.BBB     ANOP
&LL      SETA  K'&FLD(&B)+2
FLD&X    DC    CL&LL'(&FLD(&B)'
.CCC     ANOP
&B       SETA  &B+1
         AIF   (&B EQ &N).DDD
&LL      SETA  K'&FLD(&B)+1
         DC    CL&LL'&FLD(&B)'
         AGO   .CCC
.DDD     ANOP
&LL      SETA  K'&FLD(&B)+1
         DC    CL&LL'&FLD(&B))'
*-------------------------------------------------------------------
.EEE     ANOP
         AIF   ('&DC' EQ 'NO').DONE
&B       SETA  1
.FFF     ANOP
&FLD(&B) DC    CL&LEN' '
         AIF   (&B EQ &N).DONE
&B       SETA  &B+1
         AGO   .FFF
.DONE    ANOP
MAAA&X   DS    0H
         MEND
