         MACRO
&NAME    ALCERR &FUNC,&DD=,&DSN=
.*---------------------------------------------------------------------
.*       THIS MACRO IS CALLED BY THE 'SPFALLOC' MACRO TO PROCESS THE
.*       RESULTS OF SVC 99 AND FILL IN THE SMSG AND LMSG FIELDS.
.*
.*       MAY, 1982 - CREATED
.*---------------------------------------------------------------------
         GBLA  &SPF2
         GBLC  &SPF3
         LCLA  &X
         LCLC  &LIT,&LA
&X       SETA  &SYSNDX
         AIF   ('&SPF2' NE '0').GBSET
&SPF2    SETA  &SYSNDX
.GBSET   ANOP
*----------------------------------------------------------------------
*        DYNAMIC ALLOCATION ERROR MESSAGES
*----------------------------------------------------------------------
         MVC   LMSG,BLANKS
         MVC   SMSG,BLANKS
         MVI   LMSG,C'&FUNC'
.*
         AIF   (T'&DD EQ 'O').BBB
         MVC   LMSG+2(4),=C'DD=?'
         AIF   ('&DD'(1,1) NE '''').AAA
&LIT     SETC  '&DD'(2,K'&DD-2)
         MVC   LMSG+5(8),=CL8'&LIT'
         AGO   .BBB
.AAA     ANOP
         AIF   ('&DD'(1,1) NE '(').AAA1
         MVC   LMSG+5(8),0&DD
         AGO   .BBB
.AAA1    ANOP
&LA      SETC  'L''&DD'
         MVC   LMSG+5(&LA),&DD
.BBB     ANOP
.*
         AIF   (T'&DSN EQ 'O').DDD
         MVC   LMSG+14(4),=C'DSN='
         AIF   ('&DSN'(1,1) NE '''').CCC
&LIT     SETC  '&DSN'(2,K'&DSN-2)
         MVC   LMSG+18(44),=CL44'&LIT'
         AGO   .DDD
.CCC     ANOP
&LA      SETC  'L''&DSN'
         MVC   LMSG+18(&LA),&DSN
.DDD     ANOP
         BAL   R14,SPF2&SPF2
         AIF   ('&SPF3' EQ 'X').DONE
&SPF3    SETC  'X'
         B     M999&X
SPF2&SPF2 DS  0H
         LTR   R15,R15            ALLOCATION SUCCESSFUL ?
         BZ    MZZZ&X             YES - OK
         CVD   R15,SPFDBWRD
         UNPK  SPFDYN15,SPFDBWRD+6(2)
         OI    SPFDYN15+3,X'F0'
*
         UNPK  SPFDBWRD(5),S99ERROR(3) 1 MORE THAN FIELD SIZE
         TR    SPFDBWRD(4),TR&X-240 TRANSLATE
         MVC   SPFDYNER,SPFDBWRD   MOVE TO ERROR SLOT
*
         UNPK  SPFDBWRD(5),S99INFO(3)
         TR    SPFDBWRD(4),TR&X-240 TRANSLATE
         MVC   SPFDYNIN,SPFDBWRD
         CLC   SPFDYNER,=C'0210'    DATA SET IN USE ?
         BNE   MBBB&X             NO
         MVC   SMSG(15),=C'DATA SET IN USE'
         B     MZZZ&X
MBBB&X   EQU   *
         CLC   SPFDYNER,=C'031C'    INVALID MEMBER NAME ?
         BNE   MCCC&X             NO
         MVC   SMSG(19),=C'INVALID MEMBER NAME'
         B     MZZZ&X
MCCC&X   EQU   *
         CLC   SPFDYNER,=C'0320'    INVALID DSNAME ?
         BNE   MDDD&X             NO
         MVC   SMSG(21),=C'INVALID DATA SET NAME'
         B     MZZZ&X
MDDD&X   EQU   *
         CLC   SPFDYNER,=C'0330'    MBR SPECIFIED, DSN NOT A PDS
         BNE   MEEE&X             NO
         MVC   SMSG(17),=C'THIS IS NOT A PDS'
         B     MZZZ&X
MEEE&X   EQU   *
         CLC   SPFDYNER,=C'0440'    DATA SET NOT FOUND
         BNE   MFFF&X             NO
         MVC   SMSG(18),=C'DATA SET NOT FOUND'
         B     MZZZ&X
MFFF&X   EQU   *
         CLC   SPFDYNER,=C'1708'    DATA SET NOT CATALOGED
         BNE   MGGG&X             NO
         MVC   SMSG(22),=C'DATA SET NOT CATALOGED'
         B     MZZZ&X
MGGG&X   EQU   *
         CLC   SPFDYNER,=C'0410'    DDNAME UNAVAILABLE
         BNE   MHHH&X             NO
         MVC   SMSG(16),=C'DDNAME UNAVAILABLE'
         B     MZZZ&X
MHHH&X   EQU   *
MYYY&X   EQU   *
         MVC   SMSG(16),=C'ALLOCATION ERROR'
         MVC   SMSG+17(4),SPFDYNER
MZZZ&X   EQU   *
         BR    R14
TR&X     DC    C'0123456789ABCDEF'
ERR1&SPF2 DS    0H
         LA    R15,32
         MVC   SPFDYN15,=C'0032'
         MVC   SPFDYNER,=C'9999'
         MVC   SPFDYNIN,=C'9999'
         BR    R14
M999&X   DS    0H
.DONE    ANOP
         MEND
