         MACRO
&NAME    SPFTEMP &IDS,&ODS,&QUAL
.*---------------------------------------------------------------------
.*       THIS MACRO WILL GENERATE A DATA SET NAME FROM A USER SUPPLIED
.*       BASE NAME BY CHANGING THE LAST QUALIFIER OF THE BASE NAME.
.*
.*       MAY, 1982 - CREATED
.*
.*       FEB, 1984 - R. WILLMS-CHANGED TO CREATE A D.S. NAME USING
.*                   ZUSER.&IDS.&QUAL IF &IDS IS CODED AS 'NAME'
.*                   OR CREATE NAME OF ZUSER.&IDS.XX  WHERE XX IS THE
.*                   LAST QUALIFIER IN ZTEMPF IF &QUAL IS BLANK.
.*
.*        SPFTEMP ,TEMPDSN,'TEMP1' USE DS NAME FROM ZTEMPF AND CHANGE
.*                   THE LAST QUALIFIER TO .TEMP1
.*        SPFTEMP 'SPFTEMP4',TEMPDSN,'TEMP1' CREATE DS NAME AS
.*                   USERID.SPFTEMP4.TEMP1
.*        SPFTEMP  SPFTEMP4,TEMPDSN,'TEMP1' CHANGE THE LAST QUALIFIER
.*                   OF D.S. NAME AT LABEL SPFTEMP4 TO TEMP1.
.*        SPFTEMP  SPFTEMP4,TEMPDSN         CHANGE THE LAST QUALIFIER
.*                   OF D.S. NAME AT LABEL SPFTEMP4 TO BE THE SAME AS
.*                   THE LAST QUALIFIER IN ZTEMPF NAME.
.*---------------------------------------------------------------------
&NAME    DS    0H
         LCLA  &X
         LCLC  &INDS,&LIT,&LA
&X       SETA  &SYSNDX
&INDS    SETC  'ZTEMPF'
         AIF   (T'&IDS EQ 'O').AAA
&INDS    SETC  '&IDS'
         AIF   ('&IDS'(1,1) NE '''').AAA
         MVI   &ODS,C' '          CLEAR THE
&LA      SETC  'L''&ODS-1'
         MVC   &ODS+1(&LA),&ODS TARGET FIELD
         MVC   &ODS.(7),ZUSER      MOVE TSO USER ID TO TARGET
         XC    SPFTRT,SPFTRT      CLEAR TRT TABLE
         MVI   SPFTRT+C'.',X'FF'  DOT
         MVI   SPFTRT+C' ',X'FF'  BLANK
         LA    R15,&ODS
         TRT   0(8,R15),SPFTRT    FIND THE END OF TSO USER ID
         MVI   0(R1),C'.'         MOVE IN DOT
&LIT     SETC  '&IDS'(2,K'&IDS-2)
         LA    R15,1(R1)          POINT PAST THE DOT
         MVC   0(8,R15),=CL8'&LIT' MOVE IN SECOND QUALIFIER
         TRT   0(9,R15),SPFTRT    FIND THE END OF SECOND QUALIFIER
         MVI   0(R1),C'.'         MOVE IN DOT
         LA    R14,1(R1)          POINT PAST DOT
         AGO   .AAA8
.AAA     ANOP
         MVI   &ODS,C' '          CLEAR THE
&LA      SETC  'L''&ODS-1'
         MVC   &ODS+1(&LA),&ODS TARGET FIELD
         MVC   &ODS,&INDS         MOVE DSN TO TARGET
         XC    SPFTRT,SPFTRT      CLEAR TRT TABLE
         MVI   SPFTRT+C'.',X'FF'  DOT
         MVI   SPFTRT+C' ',X'FF'  BLANK
         LA    R15,&ODS
LPA&X    EQU   *
&LA      SETC  'L''&ODS'
         TRT   0(&LA,R15),SPFTRT FIND A DOT OR BLANK
         CLI   0(R1),C' '         WAS IT A BLANK ?
         BE    MAAA&X
         LR    R14,R1             DOT
         LA    R15,1(R1)
         B     LPA&X              KEEP GOING
MAAA&X   EQU   *
         LA    R14,1(R14)         BUMP PAST DOT
.AAA8    ANOP
         AIF   (T'&QUAL EQ 'O').CCC
         AIF   ('&QUAL'(1,1) EQ '''').BBB
         MVC   0(8,R14),=8C' '    CLEAR
&LA      SETC  'L''&QUAL'
         MVC   0(&LA,R14),&QUAL MOVE QUALIFIER
         AGO   .DONE
.BBB     ANOP
&LIT     SETC  '&QUAL'(2,K'&QUAL-2)
         MVC   0(8,R14),=CL8'&LIT'  MOVE LAST QUALIFIER
         AGO   .DONE
.CCC     LA    R15,ZTEMPF
         ST    R14,MXXX&X          SAVE POINTER FOR MOVE
         CNOP  0,4                 GET SETUP ON HALF WORD
         B     *+14
MXXX&X   DC    F'0'
MYYY&X   MVC   0(0,R14),0(R15)     EXECUTED MOVE
MBBB&X   TRT   0(44,R15),SPFTRT    FIND A DOT OR BLANK
         CLI   0(R1),C' '          WAS IS A BLANK ?
         BE    MCCC&X
         LR    R14,R1
         LA    R15,1(R1)
         B     MBBB&X              KEEP GOING
MCCC&X   LA    R15,1(R14)          BUMP PAST LAST DOT
         SR    R1,R15              COMPUTE LENGTH OF LAST QUALIFIER
         BCTR  R1,R0               DECREMENT FOR MOVE
         L     R14,MXXX&X          GET ADDRESS OF RECEIVING FIELD
         EX    R1,MYYY&X           MOVE IN LAST QUALIFIER
.DONE    ANOP
         MEND
